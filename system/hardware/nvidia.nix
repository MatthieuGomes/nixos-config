{
  config,
  lib,
  inputs,
  ...
} @ Inputs: let
  repo = "nix";
  branch = "unstable";
  packages = Inputs.pkgs-list.${repo}.${branch};
in {
  options = {
    nvidia.enable = lib.mkEnableOption "Enable NVIDIA proprietary driver support.";
  };
  imports = [];
  config = lib.mkIf config.nvidia.enable {
    hardware = {
      graphics = {
        enable32Bit = true;
        enable = true;
      };
      nvidia = {
        package = let
          base = config.boot.kernelPackages.nvidiaPackages.mkDriver {
            version = "590.48.01";
            sha256_64bit = "sha256-ueL4BpN4FDHMh/TNKRCeEz3Oy1ClDWto1LO/LWlr1ok=";
            openSha256 = "sha256-hECHfguzwduEfPo5pCDjWE/MjtRDhINVr4b1awFdP44=";
            settingsSha256 = "sha256-4SfCWp3swUp+x+4cuIZ7SA5H7/NoizqgPJ6S9fm90fA=";
            persistencedSha256 = "";
          };
          cachyos-nvidia-patch = packages.fetchpatch {
            url = "https://raw.githubusercontent.com/CachyOS/CachyOS-PKGBUILDS/master/nvidia/nvidia-utils/kernel-6.19.patch";
            sha256 = "sha256-YuJjSUXE6jYSuZySYGnWSNG5sfVei7vvxDcHx3K+IN4=";
          };

          # Patch the appropriate driver based on config.hardware.nvidia.open
          driverAttr =
            if config.hardware.nvidia.open
            then "open"
            else "bin";
        in
          base
          // {
            ${driverAttr} = base.${driverAttr}.overrideAttrs (oldAttrs: {
              patches = (oldAttrs.patches or []) ++ [cachyos-nvidia-patch];
            });
          };

        open = true;
        modesetting.enable = true;
        nvidiaSettings = true;
        prime = {
          intelBusId = "PCI:0@0:2:0";
          nvidiaBusId = "PCI:0@1:0:0";
          offload = {
            enable = true;
            enableOffloadCmd = true;
          };
          sync.enable = false;
          reverseSync.enable = false;
          # allowExternalGpus = false;
        };
      };
    };
    services.xserver.videoDrivers = [
      "nvidia"
      "modesetting"
    ];
  };
}
